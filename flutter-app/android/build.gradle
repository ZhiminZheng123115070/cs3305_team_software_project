buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
// AGP 8+: Auto-set namespace for plugins that do not declare it (e.g. webview_flutter_android); must register before evaluationDependsOn
subprojects { subproject ->
    subproject.afterEvaluate {
        if (subproject.plugins.hasPlugin("com.android.library")) {
            subproject.android {
                if (namespace == null || namespace.isEmpty()) {
                    def fallback = "io.flutter.plugins." + subproject.name.replaceAll("-", "_")
                    namespace = subproject.group != null ? subproject.group.toString() : fallback
                }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
